; First Name: Emmet
; Last Name: Stanevich
; ID: E01705769
; Assignment #3
; 3/13/19
; This program will scan the contents of x7000 to x7009, and order them from smallest to largest
;R0 <- SCROLLING POINTER THROUGHOUT THE ARRAY
;R1 <- HOLDS THE VALUE STORED AT THE ADDRESS DESIGNATED BY R0
;R2 <- HOLDS THE CURRENT SMALLEST VALUE
;R3 <- HOLDS THE 2'S COMPLEMENT OF THE VALUE STORED IN R1
;R4 <- A COUNTER THAT TRACKS HOW MANY VALUES ARE LEFT IN THE ARRAY
;R5 <- HOLDS THE FIRST VALUE IN THE CURRENT SUB-ARRAY
;R6 <- HOLDS THE POSITION OF THE SMALLEST VALUE IN THE ARRAY
;R7 <- HOLDS THE CURRENT STARTING POSITION OF THE ARRAY
;START <- DESIGNATES THE BEGINNING OF THE ARRAY
;ARRSIZE <- DESIGNATES THE SIZE OF THE SUB-ARRAY
.ORIG X3000
NEXT_ROUND LD R0,START	;SETS R0 TO X7000
ADD R7,R0,#0	;SETS R7 TO THE FIRST ADDRESS

LDR R5,R7,#0	;SETS R5 TO THE FIRST VALUE
LD R4,ARRSIZE;SETS THE COUNTER TO 10
ADD R4,R4,0	;CHECKS THE VALUE OF R4
BRZ FINISH	;IF R4 IS 0, THERE IS NOTHING TO DO, AND THE PROGRAM ENDS. MAKE SURE YOU RELOAD THE ASSEMBLY CODE AND THE ARRAY BEFORE ATTEMPTING TO RERUN THE PROGRAM.
LDR R2,R0,#0	;SETS R2 TO THE VALUE POINTED AT BY R0
ADD R6,R0,#0	;INITIALIZES R6 AS THE VALUE POINTED AT BY R0
LOOP	LDR R1,R0,#0	;LOADS THE VALUE OF THE ADDRESS DESIGNATED BASED ON R0 INTO R1

COMPARE	ADD R2,R2,#0	;LOOKS AT THE VALUE OF THE CURRENT SMALLEST VALUE
BRN COMPAREN	;BRANCHES IF R2 CONTAINS A NEGATIVE NUMBER
BRNZP COMPAREP	;BRANCHES IF R2 CONTAINS A NONNEGATIVE NUMBER
BREAKCOMP	ADD R0,R0,#1	;INCREMENTS R0
ADD R4,R4,#-1	;DECREMENTS COUNTER
BRP LOOP	;REPEATS IF COUNTER HASN'T REACHED 0 YET
BRNZP SWITCH

COMPAREP	ADD R1,R1,#0	;LOOKS AR THE CURRENT VALUE
BRN COMPAREPN	;BRANCHES IF R1 IS NEGATIVE
BRNZP COMPAREPP	;BRANCHES IF R1 IS POSITIVE

COMPAREPN	BRNZP SETNEW

COMPAREPP	NOT R3,R1
ADD R3,R3,#1	;STORES THE 2'S COMPLEMENT OF R1
ADD R3,R3,R2	;CALCULATES THE DIFFERENCE BETWEEN R2 AND R1
BRP SETNEW	;IF R1 IS SMALLER THAN R2, UPDATE R2 AND R6
BRNZP BREAKCOMP

COMPAREN	ADD R1,R1,#0	;LOOKS AT THE CURRENT VALUE
BRN COMPARENN	;BRANCHES IF BOTH R1 AND R2 CONTAIN NEGATIVE VALUES
BRNZP COMPARENP	;BRANCHES IF R1 IS POSITIVE

COMPARENN NOT R3,R1
ADD R3,R3,#1	;STORES THE 2'S COMPLEMENT OF R1 IN R3
ADD R3,R3,R2	;CALCULATES THE DIFFERENCE BETWEEN R2 AND R1
BRP SETNEW	;IF R1 HAS A GREATER MAGNITUDE THAN R2, UPDATE R2 AND R6
BRNZP BREAKCOMP

COMPARENP BRNZP BREAKCOMP	;IF R2 CONTAINS A NEGATIVE, WHILE R1 DOESN'T, R2 REMAINS THE SAME

SETNEW	ADD R2,R1,#0	;UPDATES R2'S VALUE TO THAT OF R1
ADD R6,R0,#0
BRNZP BREAKCOMP

SWITCH	STR R2,R7,#0	;STORES THE VALUE STORED IN R2 INTO THE LOCATION POINTED AT BY R7
STR R5,R6,#0	;STORES THE VALUE STORED IN R5 INTO THE LOCATION POINTED AT BY R6
ADD R7,R7,#1	;INCREMENTS R7
ST R7,START	;SETS START TO THE NOW INCREMENTED R7
LD R4,ARRSIZE	;RESETS R4 TO THE PREVIOUS ARRAY SIZE
ADD R4,R4,#-1	;DECREMENTS R4
BRZ FINISH	;IF 
ST R4,ARRSIZE	;SETS ARRSIZE TO THE NOW DECREMENTED R4
ADD R4,R4,#-1	;CHECKS IF R4 IS NOW 1
BRZ FINISH	;ENDS THE PROGRAM IF THE ARRAY SIZE IS 1, AS THERE IS NOTHING ELSE TO DO
BRNZP NEXT_ROUND	;STARTS A NEW CYCLE
BRNZP FINISH
FINISH	HALT
START	.FILL x7000
ARRSIZE .FILL #10
.END